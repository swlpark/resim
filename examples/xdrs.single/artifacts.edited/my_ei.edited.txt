/*******************************************************************************   
 * Copyright (c) 2012, Lingkan Gong                                                
 * All rights reserved.                                                            
 *                                                                                 
 * Redistribution and use in source and binary forms, with or without              
 * modification, are permitted provided that the following conditions are met:     
 *                                                                                 
 *  * Redistributions of source code must retain the above copyright notice,       
 *    this list of conditions and the following disclaimer.                        
 *                                                                                 
 *  * Redistributions in binary form must reproduce the above copyright notice,    
 *    this list of conditions and the following disclaimer in the documentation    
 *    and/or other materials provided with the distribution.                       
 *                                                                                 
 *  * Neither the name of the copyright holder(s) nor the names of its             
 *    contributor(s) may be used to endorse or promote products derived from this    
 *    software without specific prior written permission.                          
 *                                                                                 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND 
 * ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED   
 * WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE          
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDERS BE LIABLE FOR ANY           
 * DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES      
 * (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;    
 * LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND     
 * ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT      
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS   
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.                    
 *                                                                                 
*******************************************************************************/   

`ifndef MY_EI_EDITED_SVH
`define MY_EI_EDITED_SVH

class my_ei_edited extends rsv_error_injector#(virtual interface my_if);

	// configuration table and parameters
	`ovm_component_utils_begin(my_ei_edited)
	`ovm_component_utils_end

	// new - constructor
	function new (string name, ovm_component parent);
		super.new(name, parent);
	endfunction : new

	// Define your own tasks to inject errors.
	// You should not change the name or the prototype of these tasks.
	// Possible injections include:
	//
	//     x, all-0, all-1, negate, random, no-change

	extern virtual protected task inject_to_static_module(rsv_ei_trans tr);
	extern virtual protected task inject_to_reconfigurable_module(rsv_ei_trans tr);
	extern virtual protected task inject_to_internal_signals(rsv_ei_trans tr);
	extern virtual protected task end_of_injecting_errors(rsv_ei_trans tr);

	// User-defined, design-specific local tasks
	
	extern task ei_reset_rm();
	extern task ei_inject_to_rm(logic [31:0] ei_data);
	
endclass : my_ei_edited

//-------------------------------------------------------------------------
//-------------------------------------------------------------------------

task my_ei_edited::inject_to_static_module (rsv_ei_trans tr);
	// Default static error injection

	// Enable the static error injection and wait until the 
	// ei_vi.reconf_phase signal is asserted 

	ei_vi.sei_en <= 1'b1;
	@(posedge ei_vi.reconf_phase);
	
	// Drive undefined X values to all output signals
	// of the reconfigurable module.

	sei_vi.rc_ackn          <= 'hx;
	sei_vi.p_prdy           <= 'hx;
	sei_vi.p_data           <= 'hx;
	sei_vi.c_crdy           <= 'hx;
	sei_vi.c_cerr           <= 'hx;


endtask : my_ei_edited::inject_to_static_module

task my_ei_edited::inject_to_reconfigurable_module (rsv_ei_trans tr);
	// User-defined dynamic error injection

	// Enable the dynamic error injection and wait until the 
	// ei_vi.reconf_phase signal is asserted 

	ei_vi.dei_en <= 1'b1;
	@(posedge ei_vi.reconf_phase);
	
	// Drive undefined X values to all input signals
	// of the reconfigurable module.
	
	dei_vi.rstn             <= 'hx;
	dei_vi.rc_reqn          <= 'hx;
	dei_vi.p_crdy           <= 'hx;
	dei_vi.p_cerr           <= 'hx;
	dei_vi.c_prdy           <= 'hx;
	dei_vi.c_data           <= 'hx;
	
	// Call design-specific local tasks
	
	#800;
	ei_reset_rm();
	#200
	ei_inject_to_rm('hx);
	
endtask : my_ei_edited::inject_to_reconfigurable_module

task my_ei_edited::inject_to_internal_signals (rsv_ei_trans tr);
	// User-defined internal error injection
	
	// Pass non-default parameter to the "rsv_iei_hdl_state" API. 
	//     iei_sig_type = none, zero OR. x   
	//     iei_mem_type = none, zero OR. rand

	logic [7:0] old_rmid = ei_vi.active_module_id;
	logic [7:0] new_rmid = tr.rmid;
	
	`rsv_execute_tcl(interp, $psprintf("ReSim::rsv_iei_hdl_state %s rm%0d x rand",rr_inst,old_rmid))
	
endtask : my_ei_edited::inject_to_internal_signals

task my_ei_edited::end_of_injecting_errors (rsv_ei_trans tr);
	// Default error injection clean up routine
	
endtask : my_ei_edited::end_of_injecting_errors

task my_ei_edited::ei_reset_rm ();
	// User-defined, design-specific local task
	
	// 1. drive default value to all input signals
	dei_vi.clk   <= 1'b1;
	dei_vi.rstn  <= 1'b1;
	
	dei_vi.p_crdy <= 1'b0;
	dei_vi.p_cerr <= 1'b0;
	dei_vi.c_prdy <= 1'b0;
	dei_vi.c_data <= 32'h0;
	
	dei_vi.rc_reqn<= 1'b1;
	
	// 2. assert and release reset signal 
	dei_vi.rstn <= 1'b0; #1 dei_vi.rstn <= 1'b1;
	
endtask : my_ei_edited::ei_reset_rm

task my_ei_edited::ei_inject_to_rm (logic [31:0] ei_data);
	// User-defined, design-specific local task
	
	// 1. spawn clocking thread 
	fork : ei_clocking_thread
		dei_vi.clk = 1'b0;
		forever begin
			#2 dei_vi.clk = ~dei_vi.clk;
		end
	join_none
	
	// 2. force the rm to read 1 data
	repeat (4) @(posedge dei_vi.clk);
	#1 dei_vi.c_prdy <= 1'b1; dei_vi.c_data  <= ei_data;
	
	@(posedge dei_vi.clk iff (dei_vi.c_crdy == 1'b1) );
	#1 dei_vi.c_prdy <= 1'b0; dei_vi.c_data  <= 0;
	
	// 3. disable the clocking thread
	repeat (4) @(posedge dei_vi.clk);
	disable ei_clocking_thread;
	
endtask : my_ei_edited::ei_inject_to_rm

`endif // MY_EI_EDITED_SVH



